buildscript {
    dependencies {
        classpath "gradle.plugin.org.detoeuf:swagger-codegen-plugin:1.7.4"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE"
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

apply plugin: 'application'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: "org.detoeuf.swagger-codegen"
apply plugin: "org.springframework.boot"
apply from: 'docker.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

task packageSources(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts.archives packageSources

bootRun {
    systemProperties System.properties
}

bootRepackage {
    mainClass = 'com.nelkenstudios.usermanager.SimpleApplication'
}

springBoot {
    mainClass = 'com.nelkenstudios.usermanager.SimpleApplication'
}

configurations {
    all*.exclude group: '', module: 'servlet-api'
}


apply from: 'swagger.gradle'

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-actuator")
    runtime 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'com.google.guava:guava:18.0'


    swaggerApi (group:"com.navid.usermanager", name: 'contract', version:"${supported_api_version}", ext:"yml")

    compile 'org.springframework.security.oauth:spring-security-oauth2'
    compile 'org.springframework.security:spring-security-jwt'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'io.jsonwebtoken:jjwt:0.6.0'
    // https://mvnrepository.com/artifact/org.springframework.social/spring-social-config
    compile group: 'org.springframework.social', name: 'spring-social-facebook'
    compile 'org.springframework.security.oauth:spring-security-oauth2'
    compile 'org.springframework.boot:spring-boot-starter-freemarker'

}

def ci_branch = "UNSET"
def ci_commit = "UNSET"
def ci_system = "LOCAL"
def ci_commit_short = "UNSET"
def ci_build_number = 0
if (System.getenv("TRAVIS")) {
    ci_branch = System.getenv("TRAVIS_BRANCH")
    ci_commit = System.getenv("TRAVIS_COMMIT")
    ci_system = "travis"
    ci_commit_short = System.getenv("TRAVIS_COMMIT")
    ci_build_number = System.getenv("TRAVIS_BUILD_NUMBER")
}

version = "${ci_system}_${ci_build_number}"

processResources {
    filesMatching('**/application.properties') {
        expand( COMMIT: ci_commit,
                BRANCH: ci_branch,
                COMMIT_SHORT: ci_commit_short,
                PIPELINE_COUNTER: ci_build_number,
                VERSION: project.version,
                SUPPORTED_API: project.supported_api_version
        )
    }
}
